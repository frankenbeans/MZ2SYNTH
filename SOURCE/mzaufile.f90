! ------------------------------------------------------------------------------
! MZAUFILE.F90
!
! SUN AUDIO FILE FORMAT HANDLING SUBROUTINES FOR MZ2
!
! COPYRIGHT (C) 2024 BY E. LAMPRECHT - ALL RIGHTS RESERVED.
! ------------------------------------------------------------------------------

MODULE MZAUFILE
  USE ISO_C_BINDING
  IMPLICIT NONE

  INTEGER(C_INT32_T),PARAMETER :: DFMAGIC=INT(Z'2E736E64',C_INT32_T)  ! AU TYPE
  INTEGER(C_INT32_T),PARAMETER :: HDRSIZE=28                          ! HEADER SIZE
  INTEGER(C_INT32_T),PARAMETER :: DATSIZE=INT(Z'FFFFFFFF',C_INT32_T)  ! UNKNOWN

  INTEGER,PARAMETER :: AUF_PCM_LINEAR_16B=3
  INTEGER,PARAMETER :: AUF_FLT_LINEAR_32B=6

  INTERFACE AU_WRTSMP
     MODULE PROCEDURE AU_WRTSMP_I16
     MODULE PROCEDURE AU_WRTSMP_I32
     MODULE PROCEDURE AU_WRTSMP_R32     
  END INTERFACE AU_WRTSMP

  INTERFACE NORD
     MODULE PROCEDURE NORD_I16
     MODULE PROCEDURE NORD_I32
     MODULE PROCEDURE NORD_F32
  END INTERFACE NORD
  
CONTAINS

  SUBROUTINE AU_WRTHDR(AUFN,AUFU,ENCD,SMPR,NCHN,MCBE,OVWT,STAT) 
    IMPLICIT NONE
    CHARACTER(LEN=*)   :: AUFN
    INTEGER            :: AUFU
    INTEGER(C_INT32_T) :: ENCD,SMPR,NCHN
    LOGICAL            :: MCBE,OVWT
    INTEGER            :: STAT
    INTENT(IN)    :: AUFN,AUFU,ENCD,SMPR,NCHN
    INTENT(INOUT) :: MCBE,OVWT,STAT
    ! --- VARIABLES ---
    CHARACTER(LEN=:),ALLOCATABLE :: FSTA
    ! --- EXE CODE  ---
    IF (ENCD.NE.AUF_PCM_LINEAR_16B.AND.ENCD.NE.AUF_FLT_LINEAR_32B) GOTO 900
    IF (SMPR.LT.8000.OR.SMPR.GT.48000)                             GOTO 910
    IF (NCHN.LT.1.OR.NCHN.GT.2)                                    GOTO 920
    MCBE=MACHBE()
    IF (OVWT) THEN
       FSTA='REPLACE'
    ELSE
       FSTA='NEW'
    END IF
    OPEN(FILE=AUFN,UNIT=AUFU,FORM='UNFORMATTED',ACCESS='STREAM', &
         POSITION='REWIND',ACTION='WRITE',STATUS=FSTA,IOSTAT=STAT)
    IF (STAT.NE.0) RETURN
    WRITE(AUFU,IOSTAT=STAT) &
         NORD_I32((/DFMAGIC,HDRSIZE,DATSIZE,ENCD,SMPR,NCHN,0/),MCBE)
    RETURN
    ! --- END CODE  ---
800 FORMAT('!ERR (AU_WRTHDR): INVALID',1X,A,1X,'=',I0)
900 WRITE(*,800) 'ENCD',ENCD ; STOP    
910 WRITE(*,800) 'SMPR',SMPR ; STOP
920 WRITE(*,800) 'NCHN',NCHN ; STOP    
  END SUBROUTINE AU_WRTHDR

  SUBROUTINE AU_WRTSMP_I16(AUFU,AUDS,MCBE,STAT)
    IMPLICIT NONE
    INTEGER                 :: AUFU
    INTEGER(KIND=C_INT16_T) :: AUDS(:)
    LOGICAL                 :: MCBE
    INTEGER                 :: STAT
    INTENT(IN)    :: AUFU,AUDS,MCBE
    INTENT(INOUT) :: STAT
    ! --- EXE CODE  ---
    WRITE(AUFU,IOSTAT=STAT) NORD(AUDS,MCBE)
    ! --- END CODE  ---    
  END SUBROUTINE AU_WRTSMP_I16

  SUBROUTINE AU_WRTSMP_I32(AUFU,AUDS,MCBE,STAT)
    IMPLICIT NONE
    INTEGER                 :: AUFU
    INTEGER(KIND=C_INT32_T) :: AUDS(:)
    LOGICAL                 :: MCBE
    INTEGER                 :: STAT
    INTENT(IN)    :: AUFU,AUDS,MCBE
    INTENT(INOUT) :: STAT
    ! --- EXE CODE  ---
    WRITE(AUFU,IOSTAT=STAT) NORD(AUDS,MCBE)
    ! --- END CODE  ---    
  END SUBROUTINE AU_WRTSMP_I32

  SUBROUTINE AU_WRTSMP_R32(AUFU,AUDS,MCBE,STAT)
    IMPLICIT NONE
    INTEGER                 :: AUFU
    REAL(KIND=C_FLOAT)      :: AUDS(:)
    LOGICAL                 :: MCBE
    INTEGER                 :: STAT
    INTENT(IN)    :: AUDS,MCBE
    INTENT(INOUT) :: STAT
    ! --- EXE CODE  ---
    WRITE(AUFU,IOSTAT=STAT) NORD(AUDS,MCBE)
    ! --- END CODE  ---    
  END SUBROUTINE AU_WRTSMP_R32

  SUBROUTINE AU_CLOSE(AUFU,STAT)
    IMPLICIT NONE
    INTEGER :: AUFU
    INTEGER :: STAT
    INTENT(IN)    :: AUFU
    INTENT(INOUT) :: STAT
    ! --- EXE CODE  ---
    CLOSE(AUFU,IOSTAT=STAT)
    ! --- END CODE  ---    
  END SUBROUTINE AU_CLOSE

  PURE FUNCTION MACHBE()
    IMPLICIT NONE
    LOGICAL :: MACHBE
    ! --- VARIABLES ---
    CHARACTER(KIND=C_CHAR) :: TC
    ! --- EXE CODE  ---
    TC=TRANSFER(INT(1),TC)
    MACHBE=ICHAR(TC).EQ.0
    ! --- END CODE  ---
  END FUNCTION MACHBE

  ELEMENTAL FUNCTION NORD_I16(I,MBE)
    IMPLICIT NONE
    INTEGER(KIND=C_INT16_T)            :: NORD_I16
    INTEGER(KIND=C_INT16_T),INTENT(IN) :: I
    LOGICAL,INTENT(IN)                 :: MBE
    ! --- EXE CODE ---
    NORD_I16=I
    IF (.NOT.MBE) THEN
       ! Little-endian machine
       CALL MVBITS(I,0,8,NORD_I16,8)
       CALL MVBITS(I,8,8,NORD_I16,0)
    END IF
    ! --- END CODE ---
  END FUNCTION NORD_I16

  ELEMENTAL FUNCTION NORD_I32(I,MBE)
    IMPLICIT NONE
    INTEGER(KIND=C_INT32_T)            :: NORD_I32
    INTEGER(KIND=C_INT32_T),INTENT(IN) :: I
    LOGICAL,INTENT(IN)                 :: MBE
    ! --- EXE CODE ---
    NORD_I32=I
    IF (.NOT.MBE) THEN
       ! Little-endian machine       
       CALL MVBITS(I, 0,8,NORD_I32,24)
       CALL MVBITS(I, 8,8,NORD_I32,16)
       CALL MVBITS(I,16,8,NORD_I32, 8)
       CALL MVBITS(I,24,8,NORD_I32, 0)
    END IF
    ! --- END CODE ---
  END FUNCTION NORD_I32

  ELEMENTAL FUNCTION NORD_F32(F,MBE)
    IMPLICIT NONE
    REAL(KIND=C_FLOAT)            :: NORD_F32
    REAL(KIND=C_FLOAT),INTENT(IN) :: F
    LOGICAL,INTENT(IN)            :: MBE
    ! --- VARIABLES ---
    INTEGER(KIND=C_INT32_T) :: I
    ! --- EXE CODE ---
    NORD_F32=F
    IF (.NOT.MBE) THEN
       ! Little-endian machine
       I=0 ! Shut -Wall up talking
       CALL MVBITS(TRANSFER(F,I), 0,8,I,24)
       CALL MVBITS(TRANSFER(F,I), 8,8,I,16)
       CALL MVBITS(TRANSFER(F,I),16,8,I, 8)
       CALL MVBITS(TRANSFER(F,I),24,8,I, 0)
       NORD_F32=TRANSFER(I,NORD_F32)
    END IF
    ! --- END CODE ---
  END FUNCTION NORD_F32
 
END MODULE MZAUFILE
